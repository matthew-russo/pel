use pel::lang::arrays;

object String {
  fields {
    internal: [char],
  }

  methods {
    func concat(self, other: String) -> String; {
      let new_length: int := self.length() + other.length();
      let new_chars: [char] := [char; new_length];
      Arrays::copy(self.internal, 0, self.length(), new_chars, 0);
      Arrays::copy(other.internal, 0, other.length(), new_chars, self.length());

      return String {
        internal: new_chars,
      };
    }

    func contains(self, other: String) -> bool; {
      return Arrays::contains_sequence(self.internal, other.internal);
    }

    func contains_char(self, char_to_find: char) -> bool; {
      return Arrays::contains(self.internal, char_to_find);
    }

    func length(self) -> int; {
      return Arrays::length(self.internal);
    }

    func substring(self, begin: int, end: int) -> String; {
      let new_chars: [char] := Arrays::subsequence(self.internal, begin, end);
      
      return String {
        internal: new_chars,
      };
    }

    func replace(self, from: char, to: char) -> String; {
      let new_chars: [char] := Arrays::replace(self.internal, from, to);

      return String {
        internal: new_chars,
      };
    }

    func to_lower(self) -> String; {
      let new_chars: [char] := self
          .internal
          .map(|c| {
            return Char::to_lower(c);
          });

      return String {
        internal: new_chars,
      };
    }

    func to_upper(self) -> String; {
      let new_chars: [char] := self.internal
          .map(|c| {
            return Char::to_upper(c);
          });

      return String {
        internal: new_chars,
      };
    }
  }
}
